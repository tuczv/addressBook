plugins {
    id 'application'
    id 'groovy'
    id 'org.openapi.generator' version '6.2.1'
}

group = 'com.github.tuczv'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_17

configurations {
    springBom

    annotationProcessor.extendsFrom springBom
    compileOnly.extendsFrom springBom
    implementation.extendsFrom springBom

    testBase.extendsFrom springBom

    testImplementation.extendsFrom testBase, implementation
    testAnnotationProcessor.extendsFrom annotationProcessor
    testCompileOnly.extendsFrom testAnnotationProcessor

    integrationTestImplementation.extendsFrom testBase, implementation
    integrationTestAnnotationProcessor.extendsFrom annotationProcessor, springBom
    integrationTestCompileOnly.extendsFrom integrationTestAnnotationProcessor, springBom

    acceptanceTestImplementation.extendsFrom testBase
    acceptanceTestAnnotationProcessor.extendsFrom springBom
    acceptanceTestCompileOnly.extendsFrom(acceptanceTestAnnotationProcessor, springBom)
}

sourceSets {
    generatedSpringRestApi {
        resources.srcDirs = ['src/openapi']
    }
    main {
        java.srcDirs += generatedSpringRestApi.java.srcDirs
    }
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
    acceptanceTest
}

repositories {
    mavenCentral()
}

dependencies {
    springBom enforcedPlatform("org.springframework.boot:spring-boot-dependencies:3.0.0")
    compileOnly group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.4'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.24'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.24'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    //implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-test'
    //implementation 'org.springframework.boot:spring-boot-devtools'
    //implementation 'org.springframework.security:spring-security-config'
    implementation 'org.mongodb:mongo-java-driver:3.12.11'
    //implementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:3.5.0'
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation group: 'org.webjars', name: 'webjars-locator-core', version: '0.52'
    implementation group: 'org.webjars', name: 'swagger-ui', version: '4.14.3'
    testImplementation 'org.testcontainers:testcontainers:1.17.4'
}

openApiGenerate {
    generatorName = 'spring'
    inputSpec = sourceSets.generatedSpringRestApi.resources.srcDirs.collect({ it.listFiles() }).flatten().first() as String
    outputDir = sourceSets.generatedSpringRestApi.java.srcDirs.first() as String
    apiFilesConstrainedTo = ['']
    modelFilesConstrainedTo = ['']
    apiPackage = "${project.group}.${project.name}.api"
    modelPackage = "${project.group}.${project.name}.api.dto"
    modelNameSuffix = 'ApiDto'
    configOptions = [
            library                : 'spring-boot',
            sourceFolder           : '',
            skipOverwrite          : false,
            skipDefaultInterface   : true,
            useTags                : true,
            interfaceOnly          : true,
            hideGenerationTimestamp: true,
    ].collectEntries { [it.key, it.value as String] }
    additionalProperties = [
            useSpringfox   : false,
            openApiNullable: false,
    ].collectEntries { [it.key, it.value as String] }
}

//tasks.compileJava.dependsOn tasks.openApiGenerate

openApiValidate {
    inputSpec = openApiGenerate.inputSpec.get()
}
tasks.check.dependsOn tasks.openApiValidate

processResources {
    with copySpec {
        from sourceSets.generatedSpringRestApi.resources.srcDirs
        into 'static'
    }
}

test {
    useJUnitPlatform()
}
